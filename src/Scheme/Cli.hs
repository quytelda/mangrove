{-# LANGUAGE DeriveFunctor     #-}
{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE LambdaCase        #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE TypeFamilies      #-}
{-# LANGUAGE ViewPatterns      #-}

module Scheme.Cli
  ( Flag(..)
  , OptionInfo(..)
  , CommandInfo(..)
  , CliScheme(..)
  , Token(..)
  , optHead
  , cmdHead
  , addHelpOptions
  ) where

import           Control.Applicative
import           Control.Monad
import           Control.Monad.Except
import           Data.Char
import           Data.Functor
import           Data.List.NonEmpty     (NonEmpty)
import qualified Data.List.NonEmpty     as NonEmpty
import           Data.String
import           Data.Text              (Text)
import qualified Data.Text              as T
import qualified Data.Text.Lazy.Builder as TLB

import           ParseTree
import           Resolve
import           Scheme
import           Scheme.Sub
import           Stream
import           Text
import           TextParser

--------------------------------------------------------------------------------
-- User Interface Descriptions

data Flag
  = LongFlag Text
  | ShortFlag Char
  deriving (Eq, Ord, Show)

instance IsString Flag where
  fromString ('-':'-':name)
    | not (null name) && all isAlphaNum name = LongFlag $ T.pack name
  fromString ['-', c]
    | isAlphaNum c = ShortFlag c
  fromString s = error $ "not a valid flag: " <> s

instance Render Flag where
  render (LongFlag s)  = "--" <> render s
  render (ShortFlag c) = "-" <> render c

data OptionInfo = OptionInfo
  { optFlags :: NonEmpty Flag
  , optHelp  :: Text
  } deriving (Eq, Ord, Show)

-- | Get a representative flag for this option (e.g. the first one).
optHead :: OptionInfo -> Flag
optHead = NonEmpty.head . optFlags

data CommandInfo = CommandInfo
  { cmdNames :: NonEmpty Text
  , cmdHelp  :: Text
  } deriving (Eq, Ord, Show)

-- | Get a representative command name for this command (e.g. the
-- first one).
cmdHead :: CommandInfo -> Text
cmdHead = NonEmpty.head . cmdNames

-- | Parsers for top-level CLI arguments such as commands and options.
data CliScheme r
  = CliHelp (NonEmpty Flag)
  | CliParameter (TextParser r)
  | CliOption OptionInfo (ParseTree SubScheme r)
  | CliCommand CommandInfo (ParseTree CliScheme r)
  deriving (Functor)

instance Render (CliScheme r) where
  render = renderParser

instance HasValency CliScheme where
  valency (CliHelp _) = Just 1
  valency (CliParameter _) = Just 1
  valency (CliOption _ subtree) = case valency subtree of
                                    Just n | n <= 0 -> Just 1
                                    _               -> Just 2
  valency (CliCommand _ subtree) = (+1) <$> valency subtree

instance Resolve CliScheme where
  resolve (CliParameter (TextParser hint _)) =
    throwError $ ExpectedError [TLB.fromText hint]
  resolve (CliOption info _) =
    throwError $ ExpectedError [render (optHead info)]
  resolve (CliCommand info _) =
    throwError $ ExpectedError [render (cmdHead info)]
  resolve (CliHelp flags) =
    throwError $ ExpectedError [render (NonEmpty.head flags)]

instance Scheme CliScheme where
  data Token CliScheme
    = LongOption Text -- ^ A long form flag (e.g. --option)
    | ShortOption Char -- ^ A short form flag (e.g. -c)
    | Bound Text -- ^ A subargument bound to an option (e.g. --opt=ARG)
    | Argument Text -- ^ A freeform argument that is not an option
    | Escaped Text -- ^ An argument escaped using '--' that can only
                   -- be consumed by 'CliParameter' parsers.
    deriving (Eq, Show)

  renderToken (LongOption s)  = "--" <> render s
  renderToken (ShortOption c) = "-" <> render c
  renderToken (Bound s)       = "subargument \"" <> render s <> "\""
  renderToken (Argument s)    = render s
  renderToken (Escaped s)     = render s

  parseTokens args =
    let (regularArgs, drop 1 -> escapedArgs) = break (== "--") args
    in concatMap argToTokens regularArgs <> fmap Escaped escapedArgs
    where
      argToTokens (T.stripPrefix "--" -> Just s) =
        case keyEqualsValue s of
          Just (k, v) -> [LongOption k, Bound v]
          Nothing     -> [LongOption s]
      argToTokens (T.stripPrefix "-" -> Just s)
        | not (T.null s) = ShortOption <$> T.unpack s
      argToTokens s = [Argument s]

  sepProd _ = " "
  sepSum _ = " | "

  renderParser (CliHelp flags) = render $ NonEmpty.head flags
  renderParser (CliParameter tp) = render $ parserHint tp
  renderParser (CliOption info subtree) =
    let rep = optHead info
        separator = case rep of
                      LongFlag _  -> "="
                      ShortFlag _ -> " "
    in render rep
       <> if valencyIs (> 0) subtree
          then separator <> render subtree
          else mempty
  renderParser (CliCommand info subtree) = "{" <> render (cmdHead info) <> " "
                                           <> render subtree <> "}"

  activate (CliHelp flags) = do
    peek >>= \case
      LongOption  s -> guard $ LongFlag  s `elem` flags
      ShortOption c -> guard $ ShortFlag c `elem` flags
      _ -> empty
    pop_
    requestHelp
  activate (CliParameter tp) = do
    text <- peek >>= \case
      Argument s -> pure s
      Escaped s  -> pure s
      _          -> empty

    withContext (render (parserHint tp) <> " parameter") $
      pop_ *> runTextParser tp text
  activate (CliOption info subtree) = do
    next <- peek
    case next of
      LongOption  s -> guard $ LongFlag  s `elem` optFlags info
      ShortOption c -> guard $ ShortFlag c `elem` optFlags info
      _             -> empty
    pop_

    withContext (render next <> " option") $ do
      -- Collect arguments for the subparser's stream from the next
      -- argument in the parent stream.
      let asList s = if valencyIs (> 1) subtree
                     then T.split (== ',') s
                     else [s]

      args <- peekMaybe <&> \case
        Just (Bound s)    -> asList s
        Just (Argument s) -> asList s
        _                 -> []

      -- Evaluate the subparser in a new stream context.
      (result, leftovers) <- parseTree subtree
                             (curry pure)
                             throwError
                             (const requestHelp)
                             (parseTokens args)

      -- If the subparser consumed its input, we can safely remove it
      -- the from the parent stream. However, we cannot remove partially
      -- consumed input, so in that case we throw an error.
      when (length args /= length leftovers) $
        pop_ *> mapM_ (\arg -> throwError $ "unrecognized subargument: " <> render arg) leftovers

      -- Ensure we are not leaving an unconsumed bound argument at the
      -- head of the stream.
      peekMaybe >>= \case
        Just (Bound s) -> throwError $ "unrecognized subargument: " <> render s
        _ -> pure ()

      pure result
  activate (CliCommand info subtree) = do
    next <- peek
    case next of
      Argument s -> guard $ s `elem` cmdNames info
      _          -> empty
    pop_

    withContext (render next <> " command") $
      satiate subtree
      >>= resolveLifted

addHelpOptions :: NonEmpty Flag -> ParseTree CliScheme r -> ParseTree CliScheme r
addHelpOptions flags tree = addHelp $ go tree
  where
    addHelp :: ParseTree CliScheme a -> ParseTree CliScheme a
    addHelp _tree = ParseNode (CliHelp flags) <|> _tree

    go :: ParseTree CliScheme a -> ParseTree CliScheme a
    go (ParseNode (CliCommand info subtree)) =
      ParseNode $ CliCommand info $ addHelp $ go subtree
    go (ProdNode f l r) = ProdNode f (go l) (go r)
    go (SumNode l r) = SumNode (go l) (go r)
    go (ManyNode require p) = ManyNode require (go p)
    go node = node
