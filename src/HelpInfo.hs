{-# LANGUAGE FlexibleInstances #-}
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE RecordWildCards   #-}
{-# LANGUAGE TypeFamilies      #-}

module HelpInfo
  ( renderHelpInfo
  ) where

import qualified Data.List              as List
import qualified Data.List.NonEmpty     as NonEmpty
import           Data.Map.Strict        (Map)
import qualified Data.Map.Strict        as Map
import           Data.Text              (Text)
import qualified Data.Text              as T
import qualified Data.Text.Lazy         as TL
import qualified Data.Text.Lazy.Builder as TLB

import           Scheme
import           Scheme.Cli
import           Scheme.Sub
import           ParseTree
import           Text

data OptionHelp = OptionHelp
  { ohLongs  :: TL.Text -- Column 2
  , ohShorts :: TL.Text -- Column 1
  , ohArg    :: TL.Text -- Column 3
  , ohDesc   :: TL.Text -- Column 4
  } deriving (Eq, Ord, Show)

-- | Like `Control.Monad.when` but for 'Monoid' instead of
-- 'Alternative'.
mwhen :: Monoid a => Bool -> a -> a
mwhen condition a = if condition then a else mempty

mkOptionHelp :: OptionInfo -> ParseTree SubScheme r -> OptionHelp
mkOptionHelp OptionInfo{..} subtree =
  OptionHelp
  { ohLongs  = fmtFlagList longs
  , ohShorts = fmtFlagList shorts
  , ohArg    = mwhen (valencyIs (> 0) subtree)
               $ renderLazyText subtree
  , ohDesc   = TL.fromStrict optHelp
  }
  where
    isLongFlag LongFlag{} = True
    isLongFlag _          = False
    (longs, shorts) = NonEmpty.partition isLongFlag optFlags
    fmtFlagList = TL.intercalate ", " . fmap renderLazyText

collectOptions :: ParseTree CliScheme r -> Map [CommandInfo] [OptionHelp]
collectOptions tree = go tree mempty
  where
    go :: ParseTree CliScheme r
       -> Map [CommandInfo] [OptionHelp]
       -> Map [CommandInfo] [OptionHelp]
    go (ParseNode (CliOption info _ subtree)) =
      Map.insertWith (<>) [] [mkOptionHelp info subtree]
    go (ParseNode (CliCommand info subtree)) =
      Map.union $ Map.mapKeys (info :) $ collectOptions subtree
    go (ProdNode _ l r) = go r . go l
    go (SumNode l r)    = go r . go l
    go (ManyNode _ p)   = go p
    go _                = id

fmtOptionTable :: [OptionHelp] -> Builder
fmtOptionTable xs = foldMap formatRow $ List.sort xs
  where
    maxLengthBy f = maximum $ TL.length . f <$> xs
    col1width = maxLengthBy ohShorts
    col2width = maxLengthBy ohLongs
    col3width = maxLengthBy ohArg

    formatRow OptionHelp{..} =
      TLB.fromLazyText
      $ TL.intercalate "  "
      [ TL.justifyLeft col1width ' ' ohShorts
      , TL.justifyLeft col2width ' ' ohLongs
      , TL.justifyLeft col3width ' ' ohArg
      , ohDesc
      , "\n"
      ]

fmtHeader :: [CommandInfo] -> Builder
fmtHeader [] = mempty
fmtHeader cmds@(info : _) =
  fmtCommand cmds
  <> " command: "
  <> render (cmdHelp info)
  <> "\n"
  where
    quote m = "\"" <> m <> "\""
    fmtCommand = quote . render . T.unwords . fmap cmdHead . reverse

fmtAllSections :: Map [CommandInfo] [OptionHelp] -> Builder
fmtAllSections =
  Map.foldlWithKey
  (\acc cmds ohs ->
      acc
      <> "\n"
      <> fmtHeader cmds
      <> fmtOptionTable ohs
  ) mempty

renderHelpInfo :: Text -> Text -> ParseTree CliScheme r -> Builder
renderHelpInfo name description tree =
  "Usage: " <> render name <> " " <> render tree <> "\n\n"
  <> render description <> "\n"
  <> fmtAllSections (collectOptions tree)
